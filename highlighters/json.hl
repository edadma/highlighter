highlighter
    name: JSON
    alias: json
options
    regex: dotall
templates
    default: {{ <span class="\class">\escape\text</span> }}
equates
    int_part   = '-?(0|[1-9]\d*)'
    frac_part  = '\.\d+'
    exp_part   = '[eE][+-]?\d+'
includes
    simplevalue:
        (true|false|null)\b                                                        => Keyword.Constant
        {{ int_part }}({{ frac_part + exp_part }}|{{ exp_part }}|{{ frac_part }})  => Number.Float
        {{ int_part }}                                                             => Number.Integer
        "(\\\\|\\"|[^"])*"                                                         => String.Double
    value:
        include simplevalue
        \{  => Punctuation >objectvalue
        \[  => Punctuation >arrayvalue
states
    objectattribute:
        include value
        :   => Punctuation
        ,   => Punctuation ^
        \}  => Punctuation ^2
    objectvalue:
        "(\\\\|\\"|[^"])*"  => Name.Tag >objectattribute
        \}                  => Punctuation ^
    arrayvalue:
        include value
        ,   => Punctuation
        \]  => Punctuation ^
    root:
        include value